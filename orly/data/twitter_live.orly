package #1;

SimpleUser is <[int, str]>;
HandleTime is <[str, time_pnt]>;
HandleHandleTime is <[str, str, time_pnt]>;

user_id_by_handle = (*<[handle]>::(int)) where {
  handle = given::(str);
};

handle_by_user_id = (*<[uid]>::(str)) where {
  uid = given::(int);
};

users_who_used_hashtag = (<[k.2, *k::(HandleTime).0]> as {SimpleUser}) where {
  k = keys (HandleTime) @ <[TagUsedByPersonID, hashtag, free::(int), free::(int)]>;
  hashtag = given::(str);
};

users_who_used_hashtags = (**hashtags reduce (start start_val & users_who_used_hashtag(.hashtag:that))) where {
  start_val = ((**hashtags take 1) reduce (start empty {SimpleUser} | users_who_used_hashtag(.hashtag:that)));
  hashtags = given::({str});
};

pi = acos(-1.0);

deg_to_rad = ((deg * pi) / 180.0) where {
  deg = given::(real);
};

compute_haversine_from_degrees = (d) where {
  delta_lat = rlat2 - rlat1;
  delta_lon = rlon2 - rlon1;
  a = (sin(delta_lat / 2.0) ** 2.0) + cos(rlat1) * cos(rlat2) * (sin(delta_lon / 2.0) ** 2.0);
  c = 2 * atan2(a ** 0.5, (1 - a) ** 0.5);
  d = R * c;
  rlon1 = deg_to_rad(.deg:lon1);
  rlon2 = deg_to_rad(.deg:lon2);
  rlat1 = deg_to_rad(.deg:lat1);
  rlat2 = deg_to_rad(.deg:lat2);
  lon1 = given::(real);
  lon2 = given::(real);
  lat1 = given::(real);
  lat2 = given::(real);
  R = given::(real);
};

with {
} test {
  t1: compute_haversine_from_degrees(.lon1:50.0, .lon2:51.0, .lat1:50.0, .lat2:51.0, .R:6372.8) < 131.9;
  t2: compute_haversine_from_degrees(.lon1:50.0, .lon2:51.0, .lat1:50.0, .lat2:51.0, .R:6372.8) > 131.7;
};

compute_distance_between_degrees = (compute_haversine_from_degrees(.lon1:lon1, .lon2:lon2, .lat1:lat1, .lat2:lat2, .R:6372.8)) where {
  lon1 = given::(real);
  lon2 = given::(real);
  lat1 = given::(real);
  lat2 = given::(real);
};

MatchingLocationObj is <{
  .distance:real,
  .time1:time_pnt,
  .lon1:real,
  .lat1:real,
  .time2:time_pnt,
  .lon2:real,
  .lat2:real
}>;
when_were_users_close = (nearby_when) where {
  location_history1 = user_location_history_seq(.uid:uid1);
  location_history2 = user_location_history_seq(.uid:uid2);
  nearby_when = location_history1 reduce (start empty[MatchingLocationObj] + search_cartesian(.input:that));
  search_cartesian = (res) where {
    res = location_history2 reduce (start empty [MatchingLocationObj] + ([<{.distance:distance(.lon2:that.1, .lat2:that.2), .time1:input.0, .lon1:input.1, .lat1:input.2, .time2:that.0, .lon2:that.1, .lat2:that.2}>] if (valid_time_margin(.t1:that.0, .t2:input.0) and distance(.lon2:that.1, .lat2:that.2) <= distance_margin) else empty [MatchingLocationObj]));
    valid_time_margin = (((t1 - t2 <= time_margin) if t1 > t2 else (t2 - t1 <= time_margin))) where {
      t1 = given::(time_pnt);
      t2 = given::(time_pnt);
    };
    distance = (compute_distance_between_degrees(.lon1:input.1, .lat1:input.2, .lon2:lon2, .lat2:lat2)) where {
      lon2 = given::(real);
      lat2 = given::(real);
    };
    input = given::(<[time_pnt, real, real]>);
  };
  time_margin = given::(time_diff);
  distance_margin = given::(real);
  uid1 = given::(int);
  uid2 = given::(int);
};

with {
  <[PersonLocationID, 1, {1970-01-01T00:00:00Z}, 50.0, 50.0]> <- <["jim", 1]>;
  <[PersonLocationID, 1, {1971-01-01T00:00:01Z}, 75.0, 75.0]> <- <["jim", 4]>;
  <[PersonLocationID, 2, {1970-01-01T00:00:02Z}, 50.1, 50.1]> <- <["bob", 2]>;
  <[PersonLocationID, 2, {1970-01-01T00:00:03Z}, 80.0, 80.0]> <- <["bob", 3]>;
  <[PersonLocationID, 2, {1971-01-01T00:00:04Z}, 50.1, 50.1]> <- <["bob", 5]>;
} test {
  t1: when_were_users_close(.uid1:1, .uid2:2, .time_margin:{+0T0:1:0}, .distance_margin:100.0) == [<{.distance:compute_distance_between_degrees(.lon1:50.0, .lat1:50.0, .lon2:50.1, .lat2:50.1), .time1:{1970-01-01T00:00:00Z}, .lon1:50.0, .lat1:50.0, .time2:{1970-01-01T00:00:02Z}, .lon2:50.1, .lat2:50.1}>];
  t2: when_were_users_close(.uid1:1, .uid2:2, .time_margin:{+0T0:1:0}, .distance_margin:1.0) == empty [MatchingLocationObj];
  t3: when_were_users_close(.uid1:1, .uid2:2, .time_margin:{+0T0:0:1}, .distance_margin:100.0) == empty [MatchingLocationObj];
};

/****************************
**** Top Level Functions ****
********* By Handle *********
****************************/

followers = (_followers(.uid:user_id_by_handle(.handle:handle))) where {
  handle = given::(str);
};

following = (_following(.uid:user_id_by_handle(.handle:handle))) where {
  handle = given::(str);
};

mentioned_users = (_mentioned_users(.uid:user_id_by_handle(.handle:handle))) where {
  handle = given::(str);
};

mentioned_by_users = (_mentioned_by_users(.uid:user_id_by_handle(.handle:handle))) where {
  handle = given::(str);
};

hashtags_used_by_user = (_hashtags_used_by_user(.uid:user_id_by_handle(.handle:handle))) where {
  handle = given::(str);
};

user_location_history = (_user_location_history(.uid:user_id_by_handle(.handle:handle))) where {
  handle = given::(str);
};

users_replied_to = (_users_replied_to(.uid:user_id_by_handle(.handle:handle))) where {
  handle = given::(str);
};

users_received_reply_from = (_users_received_reply_from(.uid:user_id_by_handle(.handle:handle))) where {
  handle = given::(str);
};

common_hashtags = (_common_hashtags(.uid1:user_id_by_handle(.handle:handle1), .uid2:user_id_by_handle(.handle:handle2))) where {
  handle1 = given::(str);
  handle2 = given::(str);
};

/****************************
**** Top Level Functions ****
*********** By UID **********
****************************/

_followers = (followers_seq(.uid:uid) as {SimpleUser}) where {
  uid = given::(int);
};

_following = (following_seq(.uid:uid) as {SimpleUser}) where {
  uid = given::(int);
};

_mentioned_users = (<[k.2, *k::(HandleHandleTime).1]> as {SimpleUser}) where {
  k = keys (HandleHandleTime) @ <[DidMentionUserID, uid, free::(int), free::(int)]>;
  uid = given::(int);
};

_mentioned_by_users = (<[k.2, *k::(HandleHandleTime).1]> as {SimpleUser}) where {
  k = keys (HandleHandleTime) @ <[MentionedByUserID, uid, free::(int), free::(int)]>;
  uid = given::(int);
};

_hashtags_used_by_user = (keys (HandleTime) @ <[TagUsedByPersonID, uid, free::(str), free::(int)]>.2 as {str}) where {
  uid = given::(int);
};

_user_location_history = (user_location_history_seq(.uid:uid) as [<[time_pnt, real, real]>]) where {
  uid = given::(int);
};

_users_replied_to = (<[k.2, *k::(HandleHandleTime).1]> as {SimpleUser}) where {
  k = keys (HandleHandleTime) @ <[ReplyToUserID, uid, free::(int), free::(int)]>;
  uid = given::(int);
};

_users_received_reply_from = (<[k.2, *k::(HandleHandleTime).1]> as {SimpleUser}) where {
  k = keys (HandleHandleTime) @ <[ReplyFromUserID, uid, free::(int), free::(int)]>;
  uid = given::(int);
};

_common_hashtags = (_hashtags_used_by_user(.uid:uid1) & _hashtags_used_by_user(.uid:uid2)) where {
  uid1 = given::(int);
  uid2 = given::(int);
};

_2nd_degree_followers = ((followers_seq(.uid:uid) reduce (start empty {SimpleUser} | _followers(.uid:that.0))) - {<[uid, uhandle]>}) where {
  uid = given::(int);
  uhandle = handle_by_user_id(.uid:uid);
};

_2nd_degree_following = ((following_seq(.uid:uid) reduce (start empty {SimpleUser} | _following(.uid:that.0))) - {<[uid, uhandle]>}) where {
  uid = given::(int);
  uhandle = handle_by_user_id(.uid:uid);
};

_2nd_degree_mentions = ((mentioned_users_seq(.uid:uid) reduce (start empty {SimpleUser} | _mentioned_users(.uid:that.0))) - {<[uid, uhandle]>}) where {
  uid = given::(int);
  uhandle = handle_by_user_id(.uid:uid);
};

_2nd_degree_mentioned_by = ((mentioned_by_users_seq(.uid:uid) reduce (start empty {SimpleUser} | _mentioned_by_users(.uid:that.0))) - {<[uid, uhandle]>}) where {
  uid = given::(int);
  uhandle = handle_by_user_id(.uid:uid);
};

/******************************
**** Helpers Returning Seq ****
******************************/

followers_seq = (<[k.2, *k::(<[str, str]>).1]>) where {
  k = keys (<[str, str]>) @ <[FollowedByID, uid, free::(int)]>;
  uid = given::(int);
};

following_seq = (<[k.2, *k::(<[str, str]>).1]>) where {
  k = keys (<[str, str]>) @ <[FollowingID, uid, free::(int)]>;
  uid = given::(int);
};

mentioned_users_seq = (**_mentioned_users(.uid:uid)) where {
  uid = given::(int);
};

mentioned_by_users_seq = (**_mentioned_by_users(.uid:uid)) where {
  uid = given::(int);
};

hashtags_used_by_user_seq = (**_hashtags_used_by_user(.uid:uid)) where {
  uid = given::(int);
};

users_who_used_hashtag_seq = (**users_who_used_hashtag(.hashtag:hashtag)) where {
  hashtag = given::(str);
};

user_location_history_seq = (<[location_history.2, location_history.3, location_history.4]>) where {
  location_history = keys(<[str, int]>) @ <[PersonLocationID, uid, free::(time_pnt), free::(real), free::(real)]>;
  uid = given::(int);
};

_2nd_degree_followers_with_common_hashtags_seq = (**_2nd_degree_followers(.uid:uid) if (length_of(_hashtags_used_by_user(.uid:that.0) & hashtags_for_uid) > 0)) where {
  uid = given::(int);
  hashtags_for_uid = _hashtags_used_by_user(.uid:uid);
};

_2nd_degree_following_with_common_hashtags_seq = (**_2nd_degree_following(.uid:uid) if (length_of(_hashtags_used_by_user(.uid:that.0) & hashtags_for_uid) > 0)) where {
  uid = given::(int);
  hashtags_for_uid = _hashtags_used_by_user(.uid:uid);
};

_2nd_degree_mentions_with_common_hashtags_seq = (**_2nd_degree_mentions(.uid:uid) if (length_of(_hashtags_used_by_user(.uid:that.0) & hashtags_for_uid) > 0)) where {
  uid = given::(int);
  hashtags_for_uid = _hashtags_used_by_user(.uid:uid);
};

_2nd_degree_mentioned_by_with_common_hashtags_seq = (**_2nd_degree_mentioned_by(.uid:uid) if (length_of(_hashtags_used_by_user(.uid:that.0) & hashtags_for_uid) > 0)) where {
  uid = given::(int);
  hashtags_for_uid = _hashtags_used_by_user(.uid:uid);
};

/****************************
***** Index Identifiers *****
****************************/

ReplyToTweetID = 0;
ReplyToUserID = 1;
ReplyFromUserID = 2;
DidMentionUserID = 3;
MentionedByUserID = 4;
PersonUsedTagID = 5;
TagUsedByPersonID = 6;
PersonLocationID = 7;
FollowingID = 8;
FollowedByID = 9;
PersonRetweetedID = 10;
PersonRetweetedTweetID = 11;
PersonWasRetweetedID = 12;
TweetWasRetweetedID = 13;
