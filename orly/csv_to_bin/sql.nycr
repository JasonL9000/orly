/* <orly/csv_to_bin/sql.nycr>

   SQL-like grammar to define CSV table for importing.

   Copyright 2010-2014 OrlyAtomics, Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License. */

name = '[_a-zA-Z][_a-zA-Z0-9]*' pri 2;

asc_kwd     = '"asc"';
desc_kwd    = '"desc"';
key_kwd     = '"key"';
primary_kwd = '"primary"';
not_kwd     = '"not"';
null_kwd    = '"null"';
text_kwd    = '"text"';

open_paren  = '"("';
close_paren = '")"';

comma = '","';
semi  = '";"';

bool_kwd      = '"bool"';
id_kwd        = '"id"';
int_kwd       = '"int"';
real_kwd      = '"real"';
str_kwd       = '"str"';
time_pnt_kwd  = '"time_pnt"';

table -> def_seq <orly/csv_to_bin/syntax>;

opt_def_seq;
no_def_seq : opt_def_seq -> empty;
def_seq    : opt_def_seq -> def opt_def_seq;

def;
bad_def : def -> error semi;
col_def : def -> name type null semi;
primary_key_def : def ->
    primary_kwd key_kwd open_paren col_ref_list close_paren semi;
secondary_key_def : def ->
    name key_kwd open_paren col_ref_list close_paren semi;
text_def : def -> text_kwd name semi;

col_ref -> name order;
col_ref_list -> col_ref opt_col_ref_list_tail;
opt_col_ref_list_tail;
no_col_ref_list_tail : opt_col_ref_list_tail -> empty;
col_ref_list_tail    : opt_col_ref_list_tail -> comma col_ref_list;

null;
explicit_not_null : null -> not_kwd null_kwd;
explicit_null     : null -> null_kwd;
implicit_not_null : null -> empty;

order;
implicit_asc  : order -> empty;
explicit_asc  : order -> asc_kwd;
explicit_desc : order -> desc_kwd;

type;
bool_type      : type -> bool_kwd;
id_type        : type -> id_kwd;
int_type       : type -> int_kwd;
real_type      : type -> real_kwd;
str_type       : type -> str_kwd;
time_pnt_type  : type -> time_pnt_kwd;
